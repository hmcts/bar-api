
plugins {
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'
    id 'org.owasp.dependencycheck' version '1.4.5.1'
    id 'org.sonarqube' version '2.6'
    id "com.github.mazzeb.auto-version" version "0.3.0"
    id "org.ajoberstar.grgit" version "2.0.1"
}

apply plugin: "com.github.mazzeb.auto-version"
apply plugin: "org.ajoberstar.grgit"

autoversion {
    versionFile= 'version.json'
}

allprojects {
    group = 'uk.gov.hmcts.reform.bar'
}

task pushToGit {
 	doLast {
 		println 'Current Version: '+version
	    def grgit = org.ajoberstar.grgit.Grgit.open(dir: file('.'))
	    grgit.add(patterns: ['version.json'])
	    grgit.remote.list().each { println it }
	    grgit.commit(message: 'Committing Version Json File Changes')
	    grgit.push(remote: 'https://git.reform.hmcts.net/bar/bar-app.git', refsOrSpecs: ['refs/remotes/origin/master'] ,force: true) 
   }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'application'
    apply plugin: "com.github.mazzeb.auto-version"
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    mainClassName = "uk.gov.hmcts.bar.api.BarServiceApplication"

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    sonarqube {
        properties {
            property "sonar.exclusions", "**/*Instruction.java,**/ResourceNotFoundException.java,**/*Configuration.java"
        }
    }

    project(":bar-client") {
        sonarqube {
            skipProject = true
        }
    }
    project(":bar-api-contract") {
        sonarqube {
            skipProject = true
        }
    }


    test {
        finalizedBy jacocoTestReport
    }

    dependencyCheck {
        failBuildOnCVSS = 0
        suppressionFile = 'dependency-check-suppressions.xml'
    }

    repositories {
        mavenLocal()
        maven { url 'http://artifactory.reform.hmcts.net/artifactory/libs-release' }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.16.16'
        compileOnly 'com.google.code.findbugs:annotations:3.0.1'

        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:3.8.0'
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-starter:1.5.8.RELEASE'
        }

        dependencies {
            dependency 'uk.gov.hmcts.auth.checker:auth-checker-spring:1.0.0.46'
            dependency 'uk.gov.hmcts.reform:java-logging-spring:1.2.0'
            dependency 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.8'
            dependency 'com.google.guava:guava:22.0'

            dependency 'org.liquibase:liquibase-core:3.5.3'
            dependency 'org.postgresql:postgresql:9.4.1212'
            dependency 'org.hibernate:hibernate-validator:5.3.5.Final'

            dependency 'io.springfox:springfox-swagger2:2.6.0'
            dependency 'io.springfox:springfox-swagger-ui:2.6.0'
        }
    }
}
